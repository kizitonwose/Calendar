apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.kizitonwose'
def artifact = 'CalendarView'

android {
    compileSdkVersion versions.compile_sdk
    defaultConfig {
        minSdkVersion versions.min_sdk_library
        targetSdkVersion versions.target_sdk
        versionCode versions.version_code
        versionName versions.version_name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-module-name', "$group.$artifact"]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin_lang"

    // Expose ThreeTenABP so library users can use it directly.
    api "com.jakewharton.threetenabp:threetenabp:$versions.threetenabp"

    // Expose RecyclerView which is CalendarView's superclass to
    // prevent a compile error when using the library in a project:
    // Error: Supertypes of the following classes cannot be resolved.
    // Please make sure you have the required dependencies in the classpath:
    // class com.kizitonwose.calendarview.CalendarView,
    // unresolved supertypes: androidx.recyclerview.widget.RecyclerView

    // Not exposing RecyclerView also means that calling findViewById<CalendarView>
    // results in an error: Type argument is not within its bounds, Expected: View.
    // This is because CalendarView's superclass type cannot be determined.
    api "androidx.recyclerview:recyclerview:$versions.recyclerview"

    testImplementation "junit:junit:$versions.junit"
    testImplementation('org.threeten:threetenbp:1.3.2') {
        // Use threetenBP library for tests as context will
        // not be available to initialise threetenABP
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
}

